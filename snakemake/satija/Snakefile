from pathlib import Path

configfile: 'config.yml'

assert 'INPUT' in config
assert 'OUTPUT' in config
assert 'CLOUD_TARGET' in config
assert 'annotations_spleen_0510' in config

# Directory / file constants
SRC_DIR = Path("src")
RAW_DIR = Path(config['INPUT'])
PROCESSED_DIR = Path(config['OUTPUT'])

# Cloud constants
CLOUD_TARGET = config['CLOUD_TARGET']

# URL constants
CELL_ANNOTATIONS_URL = "https://vitessce-data.s3.amazonaws.com/source-data/satija/annotations_spleen_0510.csv"
CELLS_URL = "https://vitessce-data.s3.amazonaws.com/source-data/satija/uf-processed.tar.xz"
CL_OBO_URL = "https://vitessce-data.s3.amazonaws.com/source-data/satija/cl.obo"

# Datasets
GLOBUS_IDS = config['annotations_spleen_0510']

# Rules
rule all:
    input:
        expand(str(PROCESSED_DIR / "{globus_id}.cells.json"), globus_id=GLOBUS_IDS),
        expand(str(PROCESSED_DIR / "{globus_id}.factors.json"), globus_id=GLOBUS_IDS),
        expand(str(PROCESSED_DIR / "{globus_id}.cell-sets.json"), globus_id=GLOBUS_IDS),

rule process_dataset:
    input:
        cells_arrow=(RAW_DIR / "uf-processed" / "{globus_id}" / "cluster_marker_genes.arrow"),
        annotations_csv=(RAW_DIR / "annotations_spleen_0510" / "{globus_id}.csv"),
        cl_obo=(RAW_DIR / "cl.obo")
    output:
        cells_json=(PROCESSED_DIR / "{globus_id}.cells.json"),
        factors_json=(PROCESSED_DIR / "{globus_id}.factors.json"),
        cell_sets_json=(PROCESSED_DIR / "{globus_id}.cell-sets.json")
    params:
        script=(SRC_DIR / "process_dataset.py")
    shell:
        '''
        python {params.script} \
            -ic {input.cells_arrow} \
            -ia {input.annotations_csv} \
            -ico {input.cl_obo} \
            -oc {output.cells_json} \
            -of {output.factors_json} \
            -ocs {output.cell_sets_json}
        '''

rule split_annotation_csv:
    input:
        (RAW_DIR / "annotations_spleen_0510.csv")
    output:
        (RAW_DIR / "annotations_spleen_0510" / "{globus_id}.csv")
    params:
        script=(SRC_DIR / "split_annotation_csv.py")
    shell:
        '''
        python {params.script} \
            -i {input} \
            -o {output} \
            -gid {wildcards.globus_id}
        '''

rule convert_h5ad_to_arrow:
    input:
        (RAW_DIR / "uf-processed" / "{globus_id}" / "cluster_marker_genes.h5ad")
    output:
        (RAW_DIR / "uf-processed" / "{globus_id}" / "cluster_marker_genes.arrow")
    params:
        script=(SRC_DIR / "convert_h5ad_to_arrow.py")
    shell:
        '''
        python {params.script} \
            -i {input} \
            -o {output}
        '''

# Download CSV file containing cell type annotations.
rule download_cell_annotations_data:
    output:
        (RAW_DIR / "annotations_spleen_0510.csv")
    params:
        file_url=CELL_ANNOTATIONS_URL
    shell:
        '''
        curl -L -o {output} {params.file_url}
        '''

# Extract contents of the TAR file.
rule untar_cells_data:
    input:
        (RAW_DIR / "uf-processed.tar.xz")
    output:
        expand(
            str(RAW_DIR / "uf-processed" / "{globus_id}" / "cluster_marker_genes.h5ad"),
            globus_id=GLOBUS_IDS
        )
    shell:
        '''
        tar -xvf {input} -C {RAW_DIR}
        '''

# Download TAR file containing UMAP clustering data.
rule download_cells_data:
    output:
        (RAW_DIR / "uf-processed.tar.xz")
    params:
        file_url=CELLS_URL
    shell:
        '''
        curl -L -o {output} {params.file_url}
        '''

rule download_cl_obo:
    output:
        (RAW_DIR / "cl.obo")
    params:
        file_url=CL_OBO_URL
    shell:
        '''
        curl -L -o {output} {params.file_url}
        '''
